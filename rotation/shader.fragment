#version 300 es

precision highp float;

out vec4 outColour;

uniform vec2 u_resolution;
uniform float u_time;
uniform vec2 u_mouse;

vec3 rainbow(in float t);
mat2 rotate(float angle);
float line(in vec2 p, in vec2 a, in vec2 b, in float w);

void main() {
    vec2 uv = (gl_FragCoord.xy-0.5*u_resolution) / max(u_resolution.x,u_resolution.y);
    vec2 id = floor(uv*70.0)*0.1;
    uv = fract(uv*70.0)-0.5;
    uv *= rotate(sin(id.x-2.0*id.y-u_time));

    float l = line(uv, vec2(-0.4,0.0), vec2(0.4,0.0), 0.05);

    vec3 colour = rainbow(sin(id.x-2.0*id.y-u_time))*l;

    outColour = vec4(colour,1.0);
}

vec3 rainbow(in float t) {
    vec3 a = vec3(0.9);
    vec3 b = vec3(0.25);
    vec3 c = vec3(1.0);
    vec3 d = vec3(0.0,0.33,0.67);

    return a + b*cos(6.28318*(c*t+d));
}

mat2 rotate(float angle) {
    float s = sin(angle);
    float c = cos(angle);
    return mat2(c, -s, s, c);
}

float line(in vec2 p, in vec2 a, in vec2 b, in float w) {
    vec2 pa = p - a;
    vec2 ba = b - a;
    float h = clamp(dot(pa,ba)/dot(ba,ba), 0.0, 1.0);
    return 1.0 - smoothstep(w, w+0.01, length(pa - ba*h));
}